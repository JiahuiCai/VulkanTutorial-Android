apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')
def valLayerBinDir = "${ndkDir}/sources/third_party/vulkan/src/build-android/jniLibs"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.github.piasy.vulkantutorial"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions -std=c++11 -Werror -Wno-unused-variable"
            }
        }

        ndk.abiFilters = ['armeabi-v7a']
    }

    sourceSets {
        debug {
            ndkDir = 'src/main/cpp'
            jniLibs {
                // Must have ndk-r12 or better which including layer binaries
                srcDirs = ["${valLayerBinDir}"]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug

            externalNativeBuild {
                cmake {
                    cppFlags "-DUSE_DEBUG_EXTENTIONS -DVK_USE_PLATFORM_ANDROID_KHR"
                }
            }
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            externalNativeBuild {
                cmake {
                    cppFlags "-DVK_USE_PLATFORM_ANDROID_KHR"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

task conanInstall {
    def buildDir = new File("app/conan_build")
    buildDir.mkdirs()
    // if you have problems running the command try to specify the absolute
    // path to conan (Known problem in MacOSX) /usr/local/bin/conan
    def cmd = "conan install ../conanfile.txt --profile android_21_arm_clang --build missing"
    print(">> ${cmd} \n")

    def sout = new StringBuilder(), serr = new StringBuilder()
    def proc = cmd.execute(null, buildDir)
    proc.consumeProcessOutput(sout, serr)
    proc.waitFor()
    println "$sout $serr"
    if (proc.exitValue() != 0) {
        throw new Exception("out> $sout err> $serr" + "\nCommand: ${cmd}")
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
}
